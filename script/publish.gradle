apply plugin: 'maven-publish'

publishing {
    publications {
        atlantis_extra(MavenPublication) {
            groupId = 'com.echsylon.atlantis'
            artifactId = 'atlantis-extra'
            version = android.defaultConfig.versionName

            // The publication doesn't know about our dependencies for the Android gradle plugin,
            // so we have to manually add them to the pom
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                // Iterate over the compile dependencies (we don't want the test ones), adding a
                // <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
            
            // Source code bundle, from task defined in 'jar.gradle'
            artifact(sourceJar)

            // JavaDoc bundle, from task defined in 'jar.gradle'
            artifact(javadocJar)

            // The actual library, from task defined in 'jar.gradle'
            def fileName = "${archivesBaseName}-${version}.aar".replace(' ', '-').toLowerCase()
            artifact("${buildDir}/outputs/aar/${fileName}")
        }
    }
}

